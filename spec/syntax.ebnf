(* 
 * Inltify message syntax v0.1
 * (vue-i18n compatible)
 *)

(* main *)
Resource ::= Plural | Mesasge;

(* main structure *)
Plural ::= Message Space* "|" Space* (Mesasge (Space* "|" Space*)?)+;
Mesasge ::= MessageElement+;

(* elements *)
MessageElement ::= (Text | NamedElement | ListElement | LinkedElement)+;
NamedElement ::= (Text? Named Text?)+;
ListElement ::= (Text? List Text?)+;
LinkedElement ::= (Text? Linked Text?)+;

(* primitives *)
Text ::= TextChar+;
Key ::= AnyChar+;
Named ::= "%"? "{" Space? (Identifier) Space? "}";
List ::= "{" Space? (Digits) Space? "}";
Linked ::= "@" (LinkedModifier?) ":" "("? (Key | (Named | List)+) ")"?;
LinkedModifier ::= "." Identifier;

(* characters *)
AnyChar ::= [#x0-#xD7FF] | [#xE000-#xFFFD] | [#x10000-#x10FFFF];  (* Unicode character *)
SpecialChar ::= "{" | "}" | "|";
TextChar ::= AnyChar - SpecialChar - LineEnd;

(* number *)
Digits ::= [0-9]+;

(* identifier *)
Identifier ::= [a-zA-Z_] [a-zA-Z0-9_$]*;

(* whitespaces *)
SpaceInline ::= #x0020; (* "\u0020" *)
Tab ::= #x0009; (* \n0009 *)
LineEnd ::= #x000D#x000A | #x000A; (* "\u000D\u000A" | "\u000A" *)
Space ::= (Tab | SpaceInline | LineEnd)+;
